#Использовать 1bdd
#Использовать 1testrunner
#Использовать fs

Функция ПрогнатьФичи(Знач КаталогФайловПокрытия, Знач ПутьФич = "features", Знач ПутьОтчетаJUnit = "./bdd-log.xml")

	КаталогФич = ОбъединитьПути(".", ПутьФич);

	Файл_КаталогФич = Новый Файл(КаталогФич);

	ИсполнительБДД = Новый ИсполнительБДД;

	Если ЗначениеЗаполнено(КаталогФайловПокрытия) Тогда
		ИсполнительБДД.СохранитьВКонтекст("ПризнакСтатистикиСкриптовOnescript", Новый Файл(КаталогФайловПокрытия));
	КонецЕсли;

	РезультатыВыполнения = ИсполнительБДД.ВыполнитьФичу(Файл_КаталогФич, Файл_КаталогФич);
	ИтоговыйРезультатВыполнения = ИсполнительБДД.ПолучитьИтоговыйСтатусВыполнения(РезультатыВыполнения);

	СтатусыВыполнения = ИсполнительБДД.ВозможныеСтатусыВыполнения();

	СтатусВыполнения = СтатусыВыполнения.НеВыполнялся;
	Если РезультатыВыполнения.Строки.Количество() > 0 Тогда

		СтатусВыполнения = ИсполнительБДД.ПолучитьИтоговыйСтатусВыполнения(РезультатыВыполнения);
		ИсполнительБДД.ПоказатьПроблемныеСценарии(РезультатыВыполнения);

		ИсполнительБДД.ВывестиИтоговыеРезультатыВыполнения(РезультатыВыполнения, Файл_КаталогФич.ЭтоКаталог());
	КонецЕсли;

	ГенераторОтчетаJUnit = Новый ГенераторОтчетаJUnit;
	ГенераторОтчетаJUnit.Сформировать(РезультатыВыполнения, СтатусВыполнения, ПутьОтчетаJUnit);

	Сообщить(СтрШаблон("Результат прогона фич <%1>. Путь %2
	|", ИтоговыйРезультатВыполнения, ПутьФич));

	Возврат ИтоговыйРезультатВыполнения <> СтатусыВыполнения.Сломался;
КонецФункции

ИмяКаталогаФайловПокрытия = "coverage";

// основной код

ТекКаталог = ТекущийКаталог();

КаталогФайловПокрытия = "";

ИспользуетсяПокрытиеКода = Ложь;
Для каждого Элемент Из АргументыКоманднойСтроки Цикл
	Если Элемент = "coverage" Тогда

		КаталогФайловПокрытия = ОбъединитьПути(ТекущийКаталог(), ".", ИмяКаталогаФайловПокрытия);
		ФС.ОбеспечитьПустойКаталог(КаталогФайловПокрытия);

		Прервать;
	КонецЕсли;
КонецЦикла;

УстановитьТекущийКаталог(ТекКаталог);

Попытка
	ФичиПрошли = ПрогнатьФичи(КаталогФайловПокрытия, "features");
Исключение
	ФичиПрошли = Ложь;
	Сообщить(СтрШаблон("Тесты поведения через 1bdd выполнены неудачно
	|%1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
КонецПопытки;

Сообщить(СтрШаблон("Результат прогона основных фич <%1>
|", ФичиПрошли));

Если Не ФичиПрошли Тогда
	ВызватьИсключение "Тестирование завершилось неудачно!";
КонецЕсли;
