#Использовать gitrunner
#Использовать tempfiles
#Использовать fs

Перем Лог;

Процедура СобратьУстановитьТекущиеПлагины(Знач Каталог, ПутьКМанифестуСборки)
	
	Лог.Информация("Сборка пакета gitsync-plugins");

	Лог.Информация("Каталог сборки <%1>", Каталог);

	КомандаOpm = Новый Команда;
	КомандаOpm.УстановитьРабочийКаталог(Каталог);
	КомандаOpm.УстановитьКоманду("opm");
	КомандаOpm.ДобавитьПараметр("build");
	КомандаOpm.ДобавитьПараметр("--mf");
	КомандаOpm.ДобавитьПараметр(ПутьКМанифестуСборки);
	КомандаOpm.ДобавитьПараметр(Каталог);
	КомандаOpm.ДобавитьЛогВыводаКоманды("task.install-opm");

	КодВозврата = КомандаOpm.Исполнить();

	Если КодВозврата <> 0  Тогда
		ВызватьИсключение КомандаOpm.ПолучитьВывод();
	КонецЕсли;

	МассивФайлов = НайтиФайлы(Каталог, "gitsync-plugins*.ospx");

	Если МассивФайлов.Количество() = 0 Тогда
		ВызватьИсключение Новый ИнформацияОбОшибке("Ошибка создания пакета gitsync-plugins", "Не найден собранный файл пакета gitsync-plugins");
	КонецЕсли;

	ФайлПлагина = МассивФайлов[0].ПолноеИмя;

	ИсполнительGitSync = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "bin/gitsync/src/cmd/gitsync.os");

	Лог.Информация("Установка плагинов из файла <%1>", ФайлПлагина);

	КомандаOpm = Новый Команда;
	КомандаOpm.УстановитьРабочийКаталог(Каталог);
	КомандаOpm.УстановитьКоманду("oscript");
	КомандаOpm.ДобавитьПараметр(ИсполнительGitSync);	
	КомандаOpm.ДобавитьПараметр("p i");
	КомандаOpm.ДобавитьПараметр("-f");
	КомандаOpm.ДобавитьПараметр(ФайлПлагина);
	КомандаOpm.ДобавитьЛогВыводаКоманды("task.install-opm");

	КодВозврата = КомандаOpm.Исполнить();

	Если КодВозврата <> 0 Тогда
		ВызватьИсключение СтрШаблон("Ошибка установки плагинов из <%1> по причине <%2>", ФайлПлагина, КомандаOpm.ПолучитьВывод());
	КонецЕсли;

	ВременныеФайлы.УдалитьФайл(ФайлПлагина);

КонецПроцедуры

Лог = Логирование.ПолучитьЛог("task.install-opm");
ПутьКМанифестуСборки = "packagedef";

СобратьУстановитьТекущиеПлагины(ОбъединитьПути(ТекущийСценарий().Каталог, ".."), ПутьКМанифестуСборки);
