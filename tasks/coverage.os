#Использовать 1commands
#Использовать fs
#Использовать coverage

ИмяПакета = "gitsync-plugins";

ИмяКаталогаФайловПокрытия = "coverage";
ИмяОбщегоФайлаПокрытия = "stat.json";
ШаблонИменФайловПокрытия = "*.json";

ФС.ОбеспечитьПустойКаталог(ИмяКаталогаФайловПокрытия);
ПутьКСтат = ОбъединитьПути(ИмяКаталогаФайловПокрытия, ИмяОбщегоФайлаПокрытия);

СистемнаяИнформация = Новый СистемнаяИнформация;
ЭтоWindows = Найти(НРег(СистемнаяИнформация.ВерсияОС), "windows") > 0;

Команда = Новый Команда;
Команда.УстановитьКоманду("oscript");
Если НЕ ЭтоWindows Тогда
	Команда.ДобавитьПараметр("-encoding=utf-8");
КонецЕсли;
Команда.ДобавитьПараметр(СтрШаблон("-codestat=%1", ПутьКСтат));
Команда.ДобавитьПараметр("tasks/test.os coverage");
Команда.ПоказыватьВыводНемедленно(Истина);

КодВозврата = Команда.Исполнить();

Файл_Стат = Новый Файл(ПутьКСтат);

// Заменяем пути в файлах на ./src
ПутьКИсходникам = ОбъединитьПути(ТекущийСценарий(), "..", "src");
КаталогИсходников = Новый Файл(ПутьКИсходникам).ПолноеИмя;
КаталогИсходников = СтрЗаменить(КаталогИсходников, "\", "\\");

// взято из gitsync
СистемнаяИнформация = Новый СистемнаяИнформация;
ОбщийКаталогДанныхПриложений = СистемнаяИнформация.ПолучитьПутьПапки(СпециальнаяПапка.ЛокальныйКаталогДанныхПриложений);
ПутьУстановкиПлагинов = ОбъединитьПути(ОбщийКаталогДанныхПриложений, "gitsync", "plugins");
ПутьИсходниковУстановленныхПлагинов = ОбъединитьПути(ПутьУстановкиПлагинов, "gitsync-plugins", "src");
ПолноеИмяКаталогаИсходниковУстановленныхПлагинов = Новый Файл(ПутьИсходниковУстановленныхПлагинов).ПолноеИмя;

ПолноеИмяКаталогаИсходниковУстановленныхПлагинов = СтрЗаменить(ПолноеИмяКаталогаИсходниковУстановленныхПлагинов, "\", "\\");

ФайлыСтатистики = НайтиФайлы(ИмяКаталогаФайловПокрытия, ШаблонИменФайловПокрытия);
Для Каждого ФайлСтатистики Из ФайлыСтатистики Цикл

	ЧтениеТекста = Новый ЧтениеТекста(ФайлСтатистики.ПолноеИмя, КодировкаТекста.UTF8NoBOM);
	Содержимое = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	
	Содержимое = СтрЗаменить(Содержимое, ПолноеИмяКаталогаИсходниковУстановленныхПлагинов, КаталогИсходников);

	ЗаписьТекста = Новый ЗаписьТекста(ФайлСтатистики.ПолноеИмя, КодировкаТекста.UTF8NoBOM);
	ЗаписьТекста.Записать(Содержимое);
	ЗаписьТекста.Закрыть();

КонецЦикла;

ПроцессорГенерации = Новый ГенераторОтчетаПокрытия();

ПроцессорГенерации.ОтносительныеПути()
				.РабочийКаталог(ИмяКаталогаФайловПокрытия)
				.ИмяФайлаСтатистики(ШаблонИменФайловПокрытия)
				.ФайлСтатистики(Файл_Стат.ПолноеИмя)
				.GenericCoverage()
				.Cobertura()
				.Clover(ИмяПакета)
				.Сформировать();

ЗавершитьРаботу(0); // TODO КодВозврата
