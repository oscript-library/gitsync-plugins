
#Использовать logos
#Использовать "./internal/tool1cd"
#Использовать tempfiles

Перем ВерсияПлагина;
Перем Лог;
Перем Обработчик;
Перем КоличествоЦикловОжиданияЛицензии;
Перем КэшПроверкиПутиКХранилищу;
Перем ЭтоХранилищеРасширения;

#Область Интерфейс_плагина

// Возвращает версию плагина
//
//  Возвращаемое значение:
//   Строка - текущая версия плагина
//
Функция Версия() Экспорт
	Возврат "1.6.0";
КонецФункции

// Возвращает приоритет выполнения плагина
//
//  Возвращаемое значение:
//   Число - приоритет выполнения плагина
//
Функция Приоритет() Экспорт
	Возврат 0;
КонецФункции

// Возвращает описание плагина
//
//  Возвращаемое значение:
//   Строка - описание функциональности плагина
//
Функция Описание() Экспорт
	Возврат "Плагин заменяет использование штатных механизмов 1С на tool1CD при синхронизации";
КонецФункции

// Возвращает подробную справку к плагину 
//
//  Возвращаемое значение:
//   Строка - подробная справка для плагина
//
Функция Справка() Экспорт
	Возврат "Справка плагина";
КонецФункции

// Возвращает имя плагина
//
//  Возвращаемое значение:
//   Строка - имя плагина при подключении
//
Функция Имя() Экспорт
	Возврат "tool1CD";
КонецФункции 

// Возвращает имя лога плагина
//
//  Возвращаемое значение:
//   Строка - имя лога плагина
//
Функция ИмяЛога() Экспорт
	Возврат "oscript.lib.gitsync.plugins.tool1CD";
КонецФункции

#КонецОбласти

#Область Подписки_на_события

Процедура ПриАктивизации(СтандартныйОбработчик) Экспорт

	Обработчик = СтандартныйОбработчик;
	КоличествоЦикловОжиданияЛицензии = Обработчик.ПолучитьКоличествоЦикловОжиданияЛицензииПоУмолчанию();

КонецПроцедуры

// Вызывается перед началом работы менеджера синхронизации
//
// Параметры:
//   ПутьКХранилищу - Строка - полный путь к хранилищу конфигурации 
//   КаталогРабочейКопии - Строка - полный путь к рабочему каталогу копии
//
Процедура ПередНачаломВыполнения(ПутьКХранилищу, КаталогРабочейКопии) Экспорт

	ЭтоХранилищеРасширения = ЗначениеЗаполнено(Обработчик.ПолучитьИмяРасширения());

КонецПроцедуры

Процедура ПриЗагрузкеВерсииХранилищаВКонфигурацию(Конфигуратор, КаталогРабочейКопии, ПутьКХранилищу, НомерВерсии, СтандартнаяОбработка) Экспорт
	
	Если НЕ ПроверитьПутьКХранилищу(ПутьКХранилищу) Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоХранилищеРасширения Тогда
		Возврат;
	КонецЕсли;

	СтандартнаяОбработка = Ложь;
	ФайлХранилища = Новый Файл(ПолучитьПутьКБазеДанныхХранилища(ПутьКХранилищу));

	ВремКаталог = ВременныеФайлы.СоздатьКаталог();
	ФайлВерсии  = ИмяФайлаВыгрузкиВерсииХранилища(ВремКаталог, НомерВерсии);
	Лог.Отладка("Выгружаем версию хранилища в файл " + ФайлВерсии);
	
	ПоНомеруВерсииСохранитьКонфигурациюСредствамиTool1CD(ФайлХранилища.ПолноеИмя, ФайлВерсии, НомерВерсии);

	Пока КоличествоЦикловОжиданияЛицензии >= 0 Цикл
		Попытка
			
			Конфигуратор.ЗагрузитьКонфигурациюИзФайла(ФайлВерсии, Ложь);
			Прервать;

		Исключение
			
			// проверим текст ошибки, если текст содержит информацию о необходимости конвертировать
			// тогда выполним конвертацию и повторно попытаемся загрузить файл
			ТекстОшибки = ВРег(Конфигуратор.ВыводКоманды());
			Если Найти(ТекстОшибки, Врег("Структура конфигурации несовместима с текущей версией программы")) Тогда
				
				Конфигуратор.СконвертироватьФайлКонфигурации(ФайлВерсии);
				Конфигуратор.ЗагрузитьКонфигурациюИзФайла(ФайлВерсии, Ложь);
				Прервать;
				
			ИначеЕсли Найти(ТекстОшибки, Врег("Не обнаружено свободной лицензии!")) Тогда
				
				ПериодОжидания = 10000;

				Лог.Ошибка(ТекстОшибки);
				Лог.Информация("Повторное подключение через 10сек. Осталось попыток: <%1>", КоличествоЦикловОжиданияЛицензии);
				Приостановить(ПериодОжидания);

			Иначе
				
				ВременныеФайлы.УдалитьФайл(ВремКаталог);
				ВызватьИсключение ТекстОшибки;
				
			КонецЕсли;
			
		КонецПопытки;

		КоличествоЦикловОжиданияЛицензии = КоличествоЦикловОжиданияЛицензии - 1;
		
	КонецЦикла;

	ВременныеФайлы.УдалитьФайл(ВремКаталог);

КонецПроцедуры

Процедура ПриПолученииТаблицыВерсий(ТаблицаВерсий, ПутьКХранилищу, НачальнаяВерсия, СтандартнаяОбработка) Экспорт
	
	Если НЕ ПроверитьПутьКХранилищу(ПутьКХранилищу) Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоХранилищеРасширения() Тогда
		Возврат;
	КонецЕсли;

	ТаблицаВерсий.Очистить();

	СтандартнаяОбработка = Ложь;

	ФайлХранилища = ПолучитьПутьКБазеДанныхХранилища(ПутьКХранилищу);
	Лог.Отладка("Файл хранилища конфигурации: " + ФайлХранилища);

	ЧтениеБазыДанных = Новый ЧтениеТаблицФайловойБазыДанных;
	ЧтениеБазыДанных.ОткрытьФайл(ФайлХранилища);
	Попытка
		ТаблицаБД = ЧтениеБазыДанных.ПрочитатьТаблицу("VERSIONS");
	Исключение
		ЧтениеБазыДанных.ЗакрытьФайл();
		ВызватьИсключение;
	КонецПопытки;

	ЧтениеБазыДанных.ЗакрытьФайл();

	ТаблицаВерсий = КонвертироватьТаблицуВерсийИзФорматаБД(ТаблицаВерсий, ТаблицаБД);
	ТаблицаВерсий.Сортировать("НомерВерсии");

	Если ТипЗнч(НачальнаяВерсия) = Тип("Число") И НачальнаяВерсия > 0 Тогда
		МассивУдаляемых = Новый Массив();
		Для Каждого ТекСтрокаВерсии Из ТаблицаВерсий Цикл
			Если ТекСтрокаВерсии.НомерВерсии < НачальнаяВерсия Тогда
				МассивУдаляемых.Добавить(ТекСтрокаВерсии);
			КонецЕсли;
		КонецЦикла;
		Для Каждого ТекСтрокаВерсии Из МассивУдаляемых Цикл
			ТаблицаВерсий.Удалить(ТекСтрокаВерсии);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

// Считывает таблицу USERS пользователей хранилища
//
Процедура ПриПолученииТаблицыПользователей(ТаблицаПользователей, ПутьКХранилищу, СтандартнаяОбработка) Экспорт

	Если НЕ ПроверитьПутьКХранилищу(ПутьКХранилищу) Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоХранилищеРасширения() Тогда
		Возврат;
	КонецЕсли;

	СтандартнаяОбработка = Ложь;
	ТаблицаПользователей.Очистить();

	ФайлХранилища = ПолучитьПутьКБазеДанныхХранилища(ПутьКХранилищу);
	Лог.Отладка("Файл хранилища конфигурации: " + ФайлХранилища);

	ЧтениеБазыДанных = Новый ЧтениеТаблицФайловойБазыДанных;
	ЧтениеБазыДанных.ОткрытьФайл(ФайлХранилища);
	Попытка
		ТаблицаБД = ЧтениеБазыДанных.ПрочитатьТаблицу("USERS");
	Исключение
		ЧтениеБазыДанных.ЗакрытьФайл();
		ВызватьИсключение;
	КонецПопытки;

	ЧтениеБазыДанных.ЗакрытьФайл();
	ТаблицаПользователей = КонвертироватьТаблицуПользователейИзФорматаБД(ТаблицаПользователей, ТаблицаБД);

КонецПроцедуры

#КонецОбласти

#Область Вспомогательные_процедуры_и_функции

Функция ЭтоХранилищеРасширения()

	Возврат ЗначениеЗаполнено(Обработчик.ПолучитьИмяРасширения());
	
КонецФункции

Функция КонвертироватьТаблицуВерсийИзФорматаБД(Знач ТаблицаВерсий, Знач ТаблицаБД)

	Для Каждого СтрокаБД Из ТаблицаБД Цикл

		Если СтрокаБД.VERDATE = "0000-00-00T00:00:00" Тогда
			Продолжить;
		КонецЕсли;

		НоваяСтрока = ТаблицаВерсий.Добавить();
		НоваяСтрока.НомерВерсии = Число(СтрокаБД.VERNUM);
		НоваяСтрока.ГУИД_Автора = СтрокаБД.USERID;
		НоваяСтрока.Тэг = СтрокаБД.CODE;

		Дата = СтрЗаменить(СтрЗаменить(СтрокаБД.VERDATE, "-", ""), ":", "");
		Дата = СтрЗаменить(Дата, "T", "");
		Дата = Дата(Дата);
		НоваяСтрока.Дата = Дата;
		НоваяСтрока.Комментарий = СтрокаБД.COMMENT;

	КонецЦикла;

	Возврат ТаблицаВерсий;
КонецФункции

Функция КонвертироватьТаблицуПользователейИзФорматаБД(Знач ТаблицаПользователей, Знач ТаблицаБД)
	
	Для Каждого СтрокаБД Из ТаблицаБД Цикл

		НоваяСтрока = ТаблицаПользователей.Добавить();
		НоваяСтрока.Автор       = СтрокаБД.NAME;
		НоваяСтрока.ГУИД_Автора = СтрокаБД.USERID;

	КонецЦикла;

	Возврат ТаблицаПользователей;

КонецФункции

Функция ПроверитьПутьКХранилищу(Знач ПутьКХранилищу)
	
	Результат = КэшПроверкиПутиКХранилищу[ПутьКХранилищу];

	Если Результат = Неопределено Тогда

		Если СтрНачинаетсяС(ВРег(ПутьКХранилищу), "TCP:")
			ИЛИ СтрНачинаетсяС(ВРег(ПутьКХранилищу), "HTTP") Тогда
			КэшПроверкиПутиКХранилищу.Вставить(ПутьКХранилищу, Ложь);
			Возврат Ложь;
		КонецЕсли;

		ФайлПутиКХранилищу = Новый Файл(ПутьКХранилищу);

		Если ФайлПутиКХранилищу.Существует() 
			И ФайлПутиКХранилищу.ЭтоКаталог() Тогда
			
			Результат = ФайлПутиКХранилищу.Существует();

		ИначеЕсли ФайлПутиКХранилищу.Существует()
			И ФайлПутиКХранилищу.Расширение = "1CD" Тогда
		
			Результат = Истина;
		
		Иначе
		
			Результат = Ложь;
		
		КонецЕсли;
	
		КэшПроверкиПутиКХранилищу.Вставить(ПутьКХранилищу, Результат);

		Если Результат Тогда
			Лог.Информация("Использую tool1CD для работы с хранилищем");
		КонецЕсли;

	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция ПолучитьПутьКБазеДанныхХранилища(Знач ПутьКХранилищу)
	
	ФайлПутиКХранилищу = Новый Файл(ПутьКХранилищу);
	Если ФайлПутиКХранилищу.Существует() 
		И ФайлПутиКХранилищу.ЭтоКаталог() Тогда
		
		ФайлБазыДанныхХранилища = ОбъединитьПути(ФайлПутиКХранилищу.ПолноеИмя, "1cv8ddb.1CD");

	ИначеЕсли ФайлПутиКХранилищу.Существует() Тогда
	
		ФайлБазыДанныхХранилища = ФайлПутиКХранилищу.ПолноеИмя;

	Иначе
	
		ВызватьИсключение "Некорректный путь к хранилищу: " + ФайлПутиКХранилищу.ПолноеИмя;
	
	КонецЕсли;

	Возврат ФайлБазыДанныхХранилища;

КонецФункции // ПолучитьПутьКБазеДанныхХранилища

Процедура ПоНомеруВерсииСохранитьКонфигурациюСредствамиTool1CD(Знач ПутьКФайлуХранилища1С, Знач ВыходнойФайл, Знач НомерВерсииХранилища)

	Логирование.ПолучитьЛог("oscript.lib.tool1cd").УстановитьУровень(Лог.Уровень());
	Лог.Отладка("Получаем файл версии <%1> из хранилища: <%2>", НомерВерсииХранилища, ПутьКФайлуХранилища1С);
	ЧтениеХранилища = Новый ЧтениеХранилищаКонфигурации;
	ЧтениеХранилища.ВыгрузитьВерсиюКонфигурации(ПутьКФайлуХранилища1С, ВыходнойФайл, НомерВерсииХранилища);
	Лог.Отладка("Версия хранилища выгружена");

КонецПроцедуры

Функция ИмяФайлаВыгрузкиВерсииХранилища(Знач Каталог, Знач НомерВерсии)
	Возврат ОбъединитьПути(Каталог,  "v" + Строка(НомерВерсии) + ".cf");
КонецФункции

Процедура Инициализация()

	ВерсияПлагина = "1.3.0";
	Лог = Логирование.ПолучитьЛог(ИмяЛога());
	КомандыПлагина = Новый Массив;
	КомандыПлагина.Добавить("sync");
	КомандыПлагина.Добавить("clone");
	КомандыПлагина.Добавить("init");

	КэшПроверкиПутиКХранилищу = Новый Соответствие;
	ЭтоХранилищеРасширения = Ложь;

КонецПроцедуры

#КонецОбласти

Инициализация();
