#Использовать logos

Перем Лог;
Перем Обработчик;
Перем МеткаЗамены;

#Область Интерфейс_плагина

// Возвращает версию плагина
//
//  Возвращаемое значение:
//   Строка - текущая версия плагина
//
Функция Версия() Экспорт
	Возврат "1.0.0";
КонецФункции

// Возвращает приоритет выполнения плагина
//
//  Возвращаемое значение:
//   Число - приоритет выполнения плагина
//
Функция Приоритет() Экспорт
	Возврат 0;
КонецФункции

// Возвращает описание плагина
// Возвращает описание плагина
//
//  Возвращаемое значение:
//   Строка - описание функциональности плагина
//
Функция Описание() Экспорт
	Возврат "Плагин добавляет функциональность замены автора коммита";
КонецФункции

// Возвращает подробную справку к плагину 
//
//  Возвращаемое значение:
//   Строка - подробная справка для плагина
//
Функция Справка() Экспорт

	ТекстСправки = 
		"Плагин позволяет заменить автора коммита в git.
		|
		|Для замены необходимо при помещении версии хранилища добавить в комментарий к версии строку:
		|
		|	--GitSyncAuthor ПользователяХранилища
		|
		|При коммите изменений в git будет выполнена замена автора версии хранилища.
		|
		|Также будет удалена строка с командой замены из комментария коммита.";

	Возврат ТекстСправки;
	
КонецФункции

// Возвращает имя плагина
//
//  Возвращаемое значение:
//   Строка - имя плагина при подключении
//
Функция Имя() Экспорт
	Возврат "replace-authors";
КонецФункции 

// Возвращает имя лога плагина
//
//  Возвращаемое значение:
//   Строка - имя лога плагина
//
Функция ИмяЛога() Экспорт
	Возврат "oscript.lib.gitsync.plugins.replace-authors";
КонецФункции

#КонецОбласти

#Область Подписки_на_события

Процедура ПриАктивизации(СтандартныйОбработчик) Экспорт

	Лог.Отладка("Активизация плагина <%1>", Имя());
	Обработчик = СтандартныйОбработчик;

КонецПроцедуры

Процедура ПередНачаломВыполнения(ПутьКХранилищу, ВходящийКаталогРабочейКопии) Экспорт

	Лог.Отладка("Начата работа плагина <%1>", Имя());

КонецПроцедуры

Процедура ПослеПолученияТаблицыВерсий(ТаблицаВерсий, ПутьКХранилищу) Экспорт
	
	ТаблицаПользователейХранилища = Обработчик.ПрочитатьТаблицуПользователейХранилища(ПутьКХранилищу);

	Для Каждого СтрокаВерсии Из ТаблицаВерсий Цикл

		СтрокВКомментарии = СтрЧислоСтрок(СтрокаВерсии.Комментарий);

		Для Счетчик = 1 По СтрокВКомментарии Цикл

			СтрокаКомментария = СтрПолучитьСтроку(СтрокаВерсии.Комментарий, Счетчик);

			НужноЗаменитьАвтора = СтрНайти(СтрокаКомментария, МеткаЗамены) > 0;

			Если НужноЗаменитьАвтора Тогда
				
				НовыйАвтор = СтрЗаменить(СтрокаКомментария, МеткаЗамены, "");
				НовыйАвтор = СокрЛП(НовыйАвтор);

				СтрокаПользователя = ТаблицаПользователейХранилища.Найти(НовыйАвтор, "Автор");

				Если НЕ СтрокаПользователя = Неопределено Тогда
				
					СтарыйАвтор = СтрокаВерсии.Автор;

					СтрокаВерсии.Автор       = СтрокаПользователя.Автор;
					СтрокаВерсии.ГУИД_Автора = СтрокаПользователя.ГУИД_Автора;
					
					СтрокаВерсии.Комментарий = СтрЗаменить(СтрокаВерсии.Комментарий, СтрокаКомментария, "");

					Лог.Информация("Заменен автор коммита с <%1> на <%2>", СтарыйАвтор, НовыйАвтор);
					
				Иначе

					ТекстИсключение = СтрШаблон(
						"В версии хранилища <%1> указан не корректный пользователь для замены <%2>",
						СтрокаВерсии.НомерВерсии,
						НовыйАвтор);
					
					ВызватьИсключение ТекстИсключение;

				КонецЕсли;
					
			КонецЕсли;

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

Процедура Инициализация()

	Лог = Логирование.ПолучитьЛог(ИмяЛога());
	
	МеткаЗамены = "--GitSyncAuthor ";

КонецПроцедуры

Инициализация();
