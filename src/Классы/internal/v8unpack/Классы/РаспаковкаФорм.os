#Использовать "../../bindata"
#Использовать logos

Перем КомпонентаГотова;
Перем Лог;
Перем ПутьКФайлу;
Перем ЭтоСборкаEXE;
Перем ЭтоWindows;

// Производит распаковку формы .bin
//
// Параметры:
//   ПутьКФайлуФормы - Строка - Путь к файлу form.bin
//   ПутьККаталогуФормы - Строка - Путь к каталогу распаковки
//
Процедура Распаковать(Знач ПутьКФайлуФормы, Знач ПутьККаталогуФормы) Экспорт
	
	Если Не КомпонентаГотова Тогда
		ПодготовитьКомпоненту();
	КонецЕсли;

	dllРаспаковать(ПутьКФайлуФормы, ПутьККаталогуФормы);

КонецПроцедуры

Процедура ПриСозданииОбъекта()
	
	Лог = Логирование.ПолучитьЛог("oscript.lib.gitsync.plugins.unpackForm");

	КомпонентаГотова = Ложь;
	ЭтоСборкаEXE = ВРег(Прав(ТекущийСценарий().Источник, 3)) = "EXE";

	СистемнаяИнформация = Новый СистемнаяИнформация;
	ЭтоWindows = Найти(НРег(СистемнаяИнформация.ВерсияОС), "windows") > 0;
	
КонецПроцедуры

Процедура dllРаспаковать(Знач ФайлРаспаковки, Знач КаталогРаспаковки)
		
	Если ЭтоСборкаEXE Тогда
		
		ВыполнитьРаспаковку(ФайлРаспаковки, КаталогРаспаковки);

	Иначе
		
		Распаковщик = Новый ЧтениеФайла8(ФайлРаспаковки);
		Распаковщик.ИзвлечьВсе(КаталогРаспаковки, Истина);
	
	КонецЕсли;

		
КонецПроцедуры

Процедура ВыполнитьРаспаковку(Знач ФайлРаспаковки, Знач КаталогРаспаковки)

	Лог.Отладка("Распаковываю файла <%1> в каталог <%2>", ФайлРаспаковки, КаталогРаспаковки);

	КомандаUnpack = Новый Команда;
	КомандаUnpack.УстановитьРабочийКаталог(КаталогРаспаковки);
	КомандаUnpack.УстановитьКоманду(ПутьКФайлу);
	КомандаUnpack.ДобавитьПараметр("-P");
	КомандаUnpack.ДобавитьПараметр(ФайлРаспаковки);
	КомандаUnpack.ДобавитьПараметр(КаталогРаспаковки);
	КомандаUnpack.ПоказыватьВыводНемедленно(Истина);

	КодВозврата = КомандаUnpack.Исполнить();

	Если КодВозврата <> 0  Тогда
		ВызватьИсключение КомандаUnpack.ПолучитьВыводКоманды();
	КонецЕсли;

КонецПроцедуры

Процедура ПодготовитьКомпоненту()
	
	ЗагрузчикДвоичныхДанных = Новый ЗагрузчикЗапакованныхФайловGitsyncPlugins;
	
	Если ЭтоСборкаEXE Тогда
		
		Если ЭтоWindows Тогда
			ПутьКФайлу = ЗагрузчикДвоичныхДанных.ПолучитьПутьКФайлу("v8unpack_d.exe");
		Иначе
			ПутьКФайлу = "v8unpack";
		КонецЕсли;
		
	Иначе

		ПутьКФайлу = ЗагрузчикДвоичныхДанных.ПолучитьПутьКФайлу("v8unpack.dll");
		//Лог.Отладка("Выполняю подключение <v8unpack.dll> из файла <%1>", ПутьКФайлу);
		ПодключитьВнешнююКомпоненту(ПутьКФайлу);
	
	КонецЕсли;

	КомпонентаГотова = Истина;

КонецПроцедуры

