#Использовать logos
#Использовать tempfiles
#Использовать fs
#Использовать semver

Перем ВерсияПлагина;
Перем Лог;
Перем Обработчик;
Перем КомандыПлагина;

Перем ИмяПроекта;
Перем ИмяМодуля;
Перем РабочееПространство;
Перем ИмяРасширения;
Перем ИмяБазовогоПроекта;

Перем ИмяФайлаДампаКонфигурации;
Перем ИмяФайлаИзменений;

#Область Интерфейс_плагина

// Возвращает версию плагина
//
//  Возвращаемое значение:
//   Строка - текущая версия плагина
//
Функция Версия() Экспорт
	Возврат ВерсияПлагина;
КонецФункции

// Возвращает приоритет выполнения плагина
//
//  Возвращаемое значение:
//   Число - приоритет выполнения плагина
//
Функция Приоритет() Экспорт
	Возврат 0;
КонецФункции

// Возвращает описание плагина
//
//  Возвращаемое значение:
//   Строка - описание функциональности плагина
//
Функция Описание() Экспорт
	Возврат "Плагин добавляет возможность выгрузки в формате EDT."
	+ " Важно: Для работы плагина необходимы установленные EDT и Ring";
КонецФункции

// Возвращает подробную справку к плагину
//
//  Возвращаемое значение:
//   Строка - подробная справка для плагина
//
Функция Справка() Экспорт
	Возврат "Справка плагина";
КонецФункции

// Возвращает имя плагина
//
//  Возвращаемое значение:
//   Строка - имя плагина при подключении
//
Функция Имя() Экспорт
	Возврат "edtExport";
КонецФункции

// Возвращает имя лога плагина
//
//  Возвращаемое значение:
//   Строка - имя лога плагина
//
Функция ИмяЛога() Экспорт
	Возврат "oscript.lib.gitsync.plugins.edtExport";
КонецФункции

#КонецОбласти

#Область Подписки_на_события

Процедура ПриАктивизации(СтандартныйОбработчик) Экспорт
	
	Обработчик = СтандартныйОбработчик;
	
КонецПроцедуры

// BSLLS:UnusedParameters-off
Процедура ПередНачаломВыполнения(ПутьКХранилищу, КаталогРабочейКопии) Экспорт
	// BSLLS:UnusedParameters-on
	
	ИмяРасширения = Обработчик.ПолучитьИмяРасширения();
	
КонецПроцедуры

Процедура ПриРегистрацииКомандыПриложения(ИмяКоманды, КлассРеализации) Экспорт
	
	Лог.Отладка("Ищу команду <%1> в списке поддерживаемых", ИмяКоманды);
	Если КомандыПлагина.Найти(ИмяКоманды) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Лог.Отладка("Устанавливаю дополнительные параметры для команды %1", ИмяКоманды);
	
	КлассРеализации.Опция("PN project-name", "", "[*edtExport] Имя проекта")
	               .ТСтрока()
	               .ВОкружении("GITSYNC_PROJECT_NAME");
	
	КлассРеализации.Опция("W workspace-location", "", "[*edtExport] расположение рабочей области")
	               .ТСтрока()
	               .ВОкружении("GITSYNC_WORKSPACE_LOCATION");
	
	КлассРеализации.Опция("BP base-project-name",
	                      "",
	                      "[*edtExport] имя базового проекта в рабочей области (для расширений))")
	               .ТСтрока()
	               .ВОкружении("GITSYNC_BASE_PROJECT_NAME");

	КлассРеализации.Опция("M module",
	                      "",
	                      "[*edtExport] имя установленного модуля edt")
	               .ТСтрока()
	               .ВОкружении("GITSYNC_MODULE");
	
КонецПроцедуры

Процедура ПриПолученииПараметров(ПараметрыКоманды) Экспорт
	
	ИмяПроекта = ПараметрыКоманды.Параметр("project-name");
	ИмяМодуля = ПараметрыКоманды.Параметр("module");
	РабочееПространство = ПараметрыКоманды.Параметр("workspace-location");
	ИмяБазовогоПроекта = ПараметрыКоманды.Параметр("base-project-name");
	
	Если ПустаяСтрока(ИмяМодуля) Тогда
		ИмяМодуля = "edt";
	КонецЕсли;

	Если Не ПустаяСтрока(ИмяРасширения)
		И Не ПустаяСтрока(ИмяБазовогоПроекта)
		И ПустаяСтрока(РабочееПространство) Тогда
		
		ВызватьИсключение "При конвертации расширений с указанием базового проекта, 
		|параметр workspace-location обязателен";
		
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяПроекта) Тогда
		ВызватьИсключение "Не заполнено имя проекта";
	КонецЕсли;
	
КонецПроцедуры

// BSLLS:UnusedParameters-off
Процедура ПередПеремещениемВКаталогРабочейКопии(
		Конфигуратор,
		КаталогРабочейКопии,
		КаталогВыгрузки,
		ПутьКХранилищу,
		НомерВерсии) Экспорт
	// BSLLS:UnusedParameters-on
	
	Лог.Отладка("Начинаю выгрузку EDT");
	Лог.Отладка("Имя проекта: %1", ИмяПроекта);
	
	ПутьКФайлуИзменений = ОбъединитьПути(КаталогВыгрузки, ИмяФайлаИзменений);
	Если ФС.ФайлСуществует(ПутьКФайлуИзменений) Тогда
		
		Лог.Отладка("Используем инкрементный импорт проекта в EDT");
		
		ДополнитьИнкрементнуюВыгрузкуПроекта(Конфигуратор, КаталогВыгрузки);
		
	Иначе
		Лог.Отладка("Используем полный импорт проекта в EDT");
	КонецЕсли;
	
	ВременноеРабочееПространство = ВременныеФайлы.СоздатьКаталог();
	Если Не ПустаяСтрока(РабочееПространство) Тогда
		ФС.КопироватьСодержимоеКаталога(РабочееПространство, ВременноеРабочееПространство);
	КонецЕсли;
	
	Лог.Отладка("Рабочее пространство EDT: %1", ВременноеРабочееПространство);
	
	КаталогПроекта = ОбъединитьПути(ВременноеРабочееПространство, ИмяПроекта);
	
	Лог.Отладка("Каталог проекта EDT: %1", КаталогПроекта);
	ФС.ОбеспечитьПустойКаталог(КаталогПроекта);
	
	Команда = КомандаКонвертации(КаталогВыгрузки, ВременноеРабочееПространство, КаталогПроекта);
	
	КодВозврата = Команда.Исполнить();
	
	Лог.Отладка("Код возврата EDT: %1", КодВозврата);
	
	ТекущийФайлВерсийМетаданных = Новый Файл(ОбъединитьПути(КаталогВыгрузки, ИмяФайлаДампаКонфигурации));
	Если ТекущийФайлВерсийМетаданных.Существует() Тогда
		
		ТекущийФайлВерсийМетаданных2 = ОбъединитьПути(КаталогПроекта, ИмяФайлаДампаКонфигурации);
		КопироватьФайл(ТекущийФайлВерсийМетаданных.ПолноеИмя,
			ТекущийФайлВерсийМетаданных2);
		
	КонецЕсли;
	
	Лог.Отладка("Очищаю каталог выгрузки");
	УдалитьФайлы(КаталогВыгрузки, "*");
	
	Лог.Отладка("Копирую каталог проекта EDT в каталог выгрузки");
	ФС.КопироватьСодержимоеКаталога(КаталогПроекта, КаталогВыгрузки);
	
КонецПроцедуры

#КонецОбласти

#Область Вспомогательные_процедуры_и_функции

// Функция - получает команду запуска утилиты ring для выполнения конвертации
//
// Возвращаемое значение:
//  Команда      - команда запуска утилиты ring для выполнения конвертации
//
Функция КомандаКонвертации(КаталогВыгрузки, РабочееПространство, КаталогПроекта)

	ВерсияEDT = ВерсияEDT();

	Если Лев(ВерсияEDT(), 4) >= "2024" Тогда
		Команда = КомандаEdtcli(КаталогВыгрузки, РабочееПространство, КаталогПроекта);
	Иначе
		Команда = КомандаRing(КаталогВыгрузки, РабочееПространство, КаталогПроекта);
	КонецЕсли;
	
	Команда.УстановитьКодировкуВывода(КодировкаТекста.ANSI);
	Команда.ДобавитьЛогВыводаКоманды("oscript.lib.gitsync.plugins.edtExport");
	Команда.УстановитьИсполнениеЧерезКомандыСистемы(Истина);
	Команда.ПоказыватьВыводНемедленно(Ложь);
	Команда.УстановитьПравильныйКодВозврата(0);

	Возврат Команда;

КонецФункции // КомандаКонвертации()

// Функция - получает команду запуска утилиты ring для выполнения конвертации
//
// Возвращаемое значение:
//  Команда      - команда запуска утилиты ring для выполнения конвертации
//
Функция КомандаRing(КаталогВыгрузки, РабочееПространство, КаталогПроекта)

	Команда = Новый Команда;
	
	Параметры = Новый Массив();
	Параметры.Добавить(СтрШаблон("--configuration-files ""%1""", КаталогВыгрузки));
	Параметры.Добавить(СтрШаблон("--workspace-location ""%1""", РабочееПространство));
	Параметры.Добавить(СтрШаблон("--project ""%1""", КаталогПроекта));
	
	Если Не ПустаяСтрока(ИмяРасширения) И Не ПустаяСтрока(ИмяБазовогоПроекта) Тогда
		Параметры.Добавить(СтрШаблон("--base-project-name ""%1""", ИмяБазовогоПроекта));
	КонецЕсли;
	
	ИсполняемыйФайл = НайтиRing();

	Команда.УстановитьСтрокуЗапуска(СтрШаблон("%1 %2 workspace import", ИсполняемыйФайл, ИмяМодуля));
	Команда.ДобавитьПараметры(Параметры);

	Возврат Команда;

КонецФункции // КомандаRing()

// Функция - получает команду запуска утилиты edtcli для выполнения конвертации
//
// Возвращаемое значение:
//  Команда      - команда запуска утилиты edtcli для выполнения конвертации
//
Функция КомандаEdtcli(КаталогВыгрузки, РабочееПространство, КаталогПроекта)

	Команда = Новый Команда;
	
	Параметры = Новый Массив();
	Параметры.Добавить(СтрШаблон("--project ""%1""", КаталогПроекта));
	Параметры.Добавить(СтрШаблон("--configuration-files ""%1""", КаталогВыгрузки));
	// Параметры.Добавить(СтрШаблон("--version ""%1""", Параметры.ВерсияПлатформы));
	
	Если Не ПустаяСтрока(ИмяРасширения) И Не ПустаяСтрока(ИмяБазовогоПроекта) Тогда
		Параметры.Добавить(СтрШаблон("--base-project-name ""%1""", ИмяБазовогоПроекта));
	КонецЕсли;
	
	ИсполняемыйФайл = НайтиEdtcli();

	Команда.УстановитьСтрокуЗапуска(СтрШаблон("%1 -data ""%2"" -command import",
	                                          ИсполняемыйФайл,
	                                          РабочееПространство));
	Команда.ДобавитьПараметры(Параметры);

	Возврат Команда;

КонецФункции // КомандаEdtcli()

// Функция - выполняет поиск утилиты ring и возвращает полный путь к утилите
//
// Возвращаемое значение:
//  Строка      - полный путь к утилите ring
//
Функция НайтиRing() Экспорт

	РасположениеRing = НайтиКоманду("ring");

	Если ЗначениеЗаполнено(РасположениеRing) Тогда
		Возврат РасположениеRing;
	КонецЕсли;

	Если ЭтоWindows() Тогда
		КаталогПрограмм = ПолучитьПеременнуюСреды("ProgramW6432");
		Расширение = ".cmd";
	Иначе
		КаталогПрограмм = "/opt";
		Расширение = "";
	КонецЕсли;
	ПутьУстановки_1CE = ОбъединитьПути(КаталогПрограмм, "1C", "1CE", "components");
	Файлы = НайтиФайлы(ПутьУстановки_1CE, СтрШаблон("ring%1", Расширение), Истина);

	Если Файлы.Количество() > 0 Тогда
		Возврат Файлы[0].ПолноеИмя;
	КонецЕсли;

	Возврат "";

КонецФункции // НайтиRing()

// Функция - выполняет поиск утилиты edtcli и возвращает полный путь к утилите
//
// Возвращаемое значение:
//  Строка      - полный путь к утилите edtcli
//
Функция НайтиEdtcli() Экспорт

	РасположениеEdtcli = НайтиКоманду("1cedtcli");

	Если ЗначениеЗаполнено(РасположениеEdtcli) Тогда
		Возврат РасположениеEdtcli;
	КонецЕсли;

	Если ЭтоWindows() Тогда
		КаталогПрограмм = ПолучитьПеременнуюСреды("ProgramW6432");
		Расширение = ".exe";
	Иначе
		КаталогПрограмм = "/opt";
		Расширение = "";
	КонецЕсли;
	ПутьУстановки_1CE = ОбъединитьПути(КаталогПрограмм, "1C", "1CE", "components");
	Файлы = НайтиФайлы(ПутьУстановки_1CE, СтрШаблон("1cedtcli%1", Расширение), Истина);

	Если Файлы.Количество() > 0 Тогда
		Возврат Файлы[0].ПолноеИмя;
	КонецЕсли;

	Возврат "";

КонецФункции // НайтиEdtcli()

// Функция - выполняет поиск указанной команды в текущем каталоге или переменной окружения PATH
//
// Параметры:
//  ИмяКоманды    - Строка    - имя команды для поиска
//
// Возвращаемое значение:
//  Строка    - полный путь к команде
//
Функция НайтиКоманду(Знач ИмяКоманды)

	ЭтоWindows = ЭтоWindows();

	Команда = Новый Команда();
	Если ЭтоWindows Тогда
		Команда.УстановитьКоманду("where");
	Иначе
		Команда.УстановитьКоманду("which");
	КонецЕсли;
	Команда.ДобавитьПараметр(ИмяКоманды);

	Команда.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
	Команда.ПоказыватьВыводНемедленно(Ложь);
	
	КодВозврата = Команда.Исполнить();

	ВыводКоманды = СокрЛП(Команда.ПолучитьВывод());

	Если КодВозврата = 0 Тогда
		Лог.Отладка("Найдена команда ""%1"": %2", ИмяКоманды, ВыводКоманды);
	Иначе
		Лог.Предупреждение("Команда ""%1"" не найдена в текущем каталоге или в переменной окружения PATH:%2%3",
		                   ИмяКоманды,
		                   Символы.ПС,
		                   ВыводКоманды);
		Возврат "";
	КонецЕсли;

	Если ФС.ФайлСуществует(ВыводКоманды) Тогда
		Возврат ВыводКоманды;
	КонецЕсли;

	Возврат "";

КонецФункции // НайтиКоманду()

// Функция - проверяет, что скрипт выполняется в среде Windows
//
// Возвращаемое значение:
//	Булево     - Истина - скрипт выполняется в среде Windows
//
Функция ЭтоWindows() Экспорт

	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат Найти(НРег(СистемнаяИнформация.ВерсияОС), "windows") > 0;

КонецФункции // ЭтоWindows()

// Функция - выполняет поиск утилиты ring и возвращает полный номер последней версии EDT
//
// Возвращаемое значение:
//  Строка      - полный номер последней версии EDT
//
Функция ВерсияEDT() Экспорт

	РасположениеEdtcli = НайтиEdtcli();

	ФайлEdtcli = Новый Файл(РасположениеEdtcli);
	КаталогEdtcli = Новый Файл(ФайлEdtcli.Путь);
	КаталогEdtcli = Новый Файл(КаталогEdtcli.Путь);

	Файлы = НайтиФайлы(КаталогEdtcli.Путь, "1cedt.ini", Истина);
	РВ = Новый РегулярноеВыражение("1c-edt-(\d+\.\d+\.\d+)\+");

	НомераВерсий = Новый Массив();

	Для Каждого ТекФайл Из Файлы Цикл
		КаталогВерсии = Новый Файл(ТекФайл.Путь);

		Совпадения = РВ.НайтиСовпадения(КаталогВерсии.Имя);

		Если Совпадения.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
	
		НомераВерсий.Добавить(Совпадения[0].Группы[1].Значение);
		Лог.Отладка("Найдена версия EDT ""%1"" в каталоге ""%2"".", Совпадения[0].Группы[1].Значение, ТекФайл.Путь);
	КонецЦикла;

	Версии.СортироватьВерсии(НомераВерсий, "УБЫВ");

	Если НомераВерсий.Количество() = 0 Тогда
		Лог.Предупреждение("Не найдена версия EDT");
	КонецЕсли;

	Лог.Отладка("Найдена версия EDT ""%1"".", НомераВерсий[0]);

	Возврат НомераВерсий[0];

КонецФункции // ВерсияEDT()

Процедура ДополнитьИнкрементнуюВыгрузкуПроекта(Конфигуратор, КаталогВыгрузки)
	
	ПутьКФайлуДополнительнойВыгрузки = ВременныеФайлы.НовоеИмяФайла();
	
	СформироватьСписокДополнительныхОбъектов(КаталогВыгрузки, ПутьКФайлуДополнительнойВыгрузки);
	
	Если ФС.ФайлСуществует(ПутьКФайлуДополнительнойВыгрузки) Тогда
		
		Параметры = Конфигуратор.ПолучитьПараметрыЗапуска();
		
		Параметры.Добавить(СтрШаблон("/DumpConfigToFiles ""%1""", КаталогВыгрузки));
		Параметры.Добавить(СтрШаблон("-listFile ""%1""", ПутьКФайлуДополнительнойВыгрузки));
		
		Конфигуратор.ВыполнитьКоманду(Параметры);
		
		ВременныеФайлы.УдалитьФайл(ПутьКФайлуДополнительнойВыгрузки);
		
	КонецЕсли;
	
КонецПроцедуры

// Возращает имя родительского объекта меданных
//
// Параметры:
//   ПолноеИмяОбъекта - Строка - Полное имя объекта метаданных.
//
//  Возвращаемое значение:
//   Строка - имя родительского объекта
//
Функция РодительОбъекта(ПолноеИмяОбъекта)
	
	ЧастиИмени = СтрРазделить(ПолноеИмяОбъекта, ".");
	
	Если ЧастиИмени.Количество() > 1 Тогда
		
		ТипОбъектаМетаданных = ЧастиИмени[0];
		ИмяОбъектаМетаданных = ЧастиИмени[1];
		
	Иначе
		Возврат ПолноеИмяОбъекта;
	КонецЕсли;
	
	Если ЭтоРодительВерхнегоУровня(ТипОбъектаМетаданных) Тогда
		Возврат ТипОбъектаМетаданных;
	Иначе
		Возврат СтрШаблон("%1.%2", ТипОбъектаМетаданных, ИмяОбъектаМетаданных);
	КонецЕсли;
	
КонецФункции

Функция ЭтоВложенныйОбъект(ПолноеИмяОбъекта)
	
	ИндексВложенногоТипа = 2;

	ЧастиИмени = СтрРазделить(ПолноеИмяОбъекта, ".");
	Если ЧастиИмени.Количество() > ИндексВложенногоТипа Тогда
		ТипВложенногоОбъекта = ЧастиИмени[ИндексВложенногоТипа];
	Иначе
		Возврат Ложь;
	КонецЕсли;

	Возврат (СтрНайти("Form,Template,Recalculation,Subsystem", ТипВложенногоОбъекта) > 0);

КонецФункции

Функция ЭтоРодительВерхнегоУровня(Родитель)
	
	Возврат (СтрНайти("Configuration,Language", Родитель) > 0);

КонецФункции

Функция ИзмененныеОбъектыМетаданных(ПутьКФайлуИзменений)
	
	РегулярноеВыражение = Новый РегулярноеВыражение("^(?>New|Modified):\s*(\S+)\s*$");
	
	ЧтениеФайла = Новый ЧтениеТекста(ПутьКФайлуИзменений);
	Совпадения = РегулярноеВыражение.НайтиСовпадения(ЧтениеФайла.Прочитать());
	ЧтениеФайла.Закрыть();
	
	ИзмененныеОбъекты = Новый Соответствие;
	ИзмененныеОбъекты.Вставить("Configuration", Новый Массив);
	ИзмененныеОбъекты.Вставить("Language", Новый Массив);
	
	Для Каждого Совпадение Из Совпадения Цикл
		
		ИмяОбъектаМетаданных = Совпадение.Группы[1].Значение;
		Родитель = РодительОбъекта(ИмяОбъектаМетаданных);
		
		ПодчиненныеОбъекты = ИзмененныеОбъекты.Получить(Родитель);
		Если ПодчиненныеОбъекты = Неопределено Тогда
			ПодчиненныеОбъекты = Новый Массив;
			ИзмененныеОбъекты.Вставить(Родитель, ПодчиненныеОбъекты);
		КонецЕсли;
		
		ПодчиненныеОбъекты.Добавить(ИмяОбъектаМетаданных);
		
	КонецЦикла;
	
	Возврат ИзмененныеОбъекты;
	
КонецФункции

Функция ДополнительныеОбъектыКВыгрузке(ИзмененныеОбъекты, ПутьКФайлуВерсийМетаданных)
	
	СписокОбъектов = Новый Массив;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ПутьКФайлуВерсийМетаданных);

	ЧтениеXML.ПерейтиКСодержимому(); // ConfigDumpInfo
	ЧтениеXML.Прочитать(); // ConfigVersions
	ЧтениеXML.Прочитать(); // Metadata

	Пока ЧтениеXML.Имя = "Metadata" Цикл

		ИмяОбъекта = ЧтениеXML.ЗначениеАтрибута("name");
		Родитель = РодительОбъекта(ИмяОбъекта);

		Если ЭтоВложенныйОбъект(ИмяОбъекта) 
			ИЛИ ИмяОбъекта = Родитель
			ИЛИ ЭтоРодительВерхнегоУровня(Родитель) Тогда

			Изменения = ИзмененныеОбъекты.Получить(Родитель);
			Если Изменения <> Неопределено И Изменения.Найти(ИмяОбъекта) = Неопределено	Тогда
				СписокОбъектов.Добавить(ИмяОбъекта);
			КонецЕсли;

		КонецЕсли;

		ЧтениеXML.Пропустить();
		ЧтениеXML.Прочитать();

	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	
	Возврат СписокОбъектов;
	
КонецФункции

Процедура СформироватьСписокДополнительныхОбъектов(КаталогВыгрузки, ПутьКФайлуДополнительнойВыгрузки)
	
	ПутьКФайлуИзменений = ОбъединитьПути(КаталогВыгрузки, ИмяФайлаИзменений);
	ПутьКФайлуВерсийМетаданных = ОбъединитьПути(КаталогВыгрузки, ИмяФайлаДампаКонфигурации);
	
	ИзмененныеОбъекты = ИзмененныеОбъектыМетаданных(ПутьКФайлуИзменений);
	ДополнительныеОбъектыКВыгрузке = ДополнительныеОбъектыКВыгрузке(ИзмененныеОбъекты, ПутьКФайлуВерсийМетаданных);
	
	Если ДополнительныеОбъектыКВыгрузке.Количество() > 0 Тогда
		
		ФайлОбъектовВыгрузки = Новый ТекстовыйДокумент();
		Для Каждого ДополнительныйОбъект Из ДополнительныеОбъектыКВыгрузке Цикл
			ФайлОбъектовВыгрузки.ДобавитьСтроку(ДополнительныйОбъект);
		КонецЦикла;
		ФайлОбъектовВыгрузки.Записать(ПутьКФайлуДополнительнойВыгрузки, КодировкаТекста.UTF8);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Инициализация()
	
	ВерсияПлагина = "1.4.0";
	Лог = Логирование.ПолучитьЛог(ИмяЛога());
	КомандыПлагина = Новый Массив;
	КомандыПлагина.Добавить("sync");
	
	ИмяРасширения = "";
	РабочееПространство = "";
	ИмяБазовогоПроекта = "";
	
	ИмяФайлаДампаКонфигурации = "ConfigDumpInfo.xml";
	ИмяФайлаИзменений = "dumplist.txt"; // файл используется в 2х плагинах - increment и edtExport
	
КонецПроцедуры

#КонецОбласти

Инициализация();
