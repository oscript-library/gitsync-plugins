#Использовать logos
#Использовать tempfiles
#Использовать fs
#Использовать semver

Перем Лог;
Перем Обработчик;
Перем КомандыПлагина;

Перем ИмяПроекта;
Перем ВерсияEDT;
Перем РабочееПространство;
Перем ИмяРасширения;
Перем ИмяБазовогоПроекта;
Перем ИспользоватьПлагин;

Перем ИмяФайлаДампаКонфигурации;
Перем ИмяФайлаИзменений;

#Область Интерфейс_плагина

// Возвращает версию плагина
//
//  Возвращаемое значение:
//   Строка - текущая версия плагина
//
Функция Версия() Экспорт
	Возврат "2.0.0";
КонецФункции

// Возвращает приоритет выполнения плагина
//
//  Возвращаемое значение:
//   Число - приоритет выполнения плагина
//
Функция Приоритет() Экспорт
	Возврат 0;
КонецФункции

// Возвращает описание плагина
//
//  Возвращаемое значение:
//   Строка - описание функциональности плагина
//
Функция Описание() Экспорт
	Возврат "Плагин добавляет возможность выгрузки в формате EDT."
	+ " Важно: Для работы плагина необходимы установленные EDT и ring\1cedtcli";
КонецФункции

// Возвращает подробную справку к плагину
//
//  Возвращаемое значение:
//   Строка - подробная справка для плагина
//
Функция Справка() Экспорт
	Возврат "Справка плагина";
КонецФункции

// Возвращает имя плагина
//
//  Возвращаемое значение:
//   Строка - имя плагина при подключении
//
Функция Имя() Экспорт
	Возврат "edtExport";
КонецФункции

// Возвращает имя лога плагина
//
//  Возвращаемое значение:
//   Строка - имя лога плагина
//
Функция ИмяЛога() Экспорт
	Возврат "oscript.lib.gitsync.plugins.edtExport";
КонецФункции

#КонецОбласти

#Область Подписки_на_события

Процедура ПриАктивизации(СтандартныйОбработчик) Экспорт
	
	Обработчик = СтандартныйОбработчик;
	
КонецПроцедуры

// BSLLS:UnusedParameters-off
Процедура ПередНачаломВыполнения(ПутьКХранилищу, КаталогРабочейКопии) Экспорт
	// BSLLS:UnusedParameters-on
	
	ИмяРасширения = Обработчик.ПолучитьИмяРасширения();
	
КонецПроцедуры

Процедура ПриРегистрацииКомандыПриложения(ИмяКоманды, КлассРеализации) Экспорт
	
	Лог.Отладка("Ищу команду <%1> в списке поддерживаемых", ИмяКоманды);
	Если КомандыПлагина.Найти(ИмяКоманды) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Лог.Отладка("Устанавливаю дополнительные параметры для команды %1", ИмяКоманды);
	
	КлассРеализации.Опция("EF edt-format", Ложь, "[*edtExport] Флаг включения выгрузки в формате EDT")
					.Флаговый();
	
	КлассРеализации.Опция("PN project-name", "", "[*edtExport] Имя проекта")
	               .ТСтрока()
	               .ВОкружении("GITSYNC_PROJECT_NAME");
	
	КлассРеализации.Опция("W workspace-location", "", "[*edtExport] расположение рабочей области")
	               .ТСтрока()
	               .ВОкружении("GITSYNC_WORKSPACE_LOCATION");
	
	КлассРеализации.Опция("BP base-project-name",
	                      "",
	                      "[*edtExport] имя базового проекта в рабочей области (для расширений))")
	               .ТСтрока()
	               .ВОкружении("GITSYNC_BASE_PROJECT_NAME");

	КлассРеализации.Опция("EDT edt-version",
	                      "",
	                      "[*edtExport] версия EDT")
	               .ТСтрока()
	               .ВОкружении("GITSYNC_EDT_VERSION");
	
КонецПроцедуры

Процедура ПриПолученииПараметров(ПараметрыКоманды) Экспорт
	
	ИспользоватьПлагин = ПараметрыКоманды.Параметр("edt-format", Ложь);
	ИспользоватьПлагин = Булево(ИспользоватьПлагин);

	ИмяПроекта = ПараметрыКоманды.Параметр("project-name");
	ВерсияEDT = ПараметрыКоманды.Параметр("edt-version");
	РабочееПространство = ПараметрыКоманды.Параметр("workspace-location");
	ИмяБазовогоПроекта = ПараметрыКоманды.Параметр("base-project-name");

	Если НЕ ИспользоватьПлагин Тогда
		Возврат;
	КонецЕсли;

	Если Не ПустаяСтрока(ИмяРасширения)
		И Не ПустаяСтрока(ИмяБазовогоПроекта)
		И ПустаяСтрока(РабочееПространство) Тогда
		
		ВызватьИсключение "При конвертации расширений с указанием базового проекта, 
		|параметр workspace-location обязателен";
		
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяПроекта) Тогда
		ВызватьИсключение "Не заполнено имя проекта";
	КонецЕсли;
	
КонецПроцедуры

// BSLLS:UnusedParameters-off
Процедура ПередПеремещениемВКаталогРабочейКопии(
		Конфигуратор,
		КаталогРабочейКопии,
		КаталогВыгрузки,
		ПутьКХранилищу,
		НомерВерсии) Экспорт
// BSLLS:UnusedParameters-on
	
	Если НЕ ИспользоватьПлагин Тогда
		Возврат;
	КонецЕсли;

	Лог.Отладка("Начинаю выгрузку EDT");
	Лог.Отладка("Имя проекта: %1", ИмяПроекта);
	
	ПутьКФайлуИзменений = ОбъединитьПути(КаталогВыгрузки, ИмяФайлаИзменений);
	Если ФС.ФайлСуществует(ПутьКФайлуИзменений) Тогда
		
		Лог.Отладка("Используем инкрементный импорт проекта в EDT");
		
		ДополнитьИнкрементнуюВыгрузкуПроекта(Конфигуратор, КаталогВыгрузки);
		
	Иначе
		Лог.Отладка("Используем полный импорт проекта в EDT");
	КонецЕсли;
	
	ВременноеРабочееПространство = ВременныеФайлы.СоздатьКаталог();
	Если Не ПустаяСтрока(РабочееПространство) Тогда
		ФС.КопироватьСодержимоеКаталога(РабочееПространство, ВременноеРабочееПространство);
	КонецЕсли;
	
	Лог.Отладка("Рабочее пространство EDT: %1", ВременноеРабочееПространство);
	
	КаталогПроекта = ОбъединитьПути(ВременноеРабочееПространство, ИмяПроекта);
	
	Лог.Отладка("Каталог проекта EDT: %1", КаталогПроекта);
	ФС.ОбеспечитьПустойКаталог(КаталогПроекта);
	
	Команда = КомандаКонвертации(КаталогВыгрузки, ВременноеРабочееПространство, КаталогПроекта);
	
	КодВозврата = Команда.Исполнить();
	
	Лог.Отладка("Код возврата команды конвертации: %1", КодВозврата);
	
	ТекущийФайлВерсийМетаданных = Новый Файл(ОбъединитьПути(КаталогВыгрузки, ИмяФайлаДампаКонфигурации));
	Если ТекущийФайлВерсийМетаданных.Существует() Тогда
		
		ТекущийФайлВерсийМетаданных2 = ОбъединитьПути(КаталогПроекта, ИмяФайлаДампаКонфигурации);
		КопироватьФайл(ТекущийФайлВерсийМетаданных.ПолноеИмя,
			ТекущийФайлВерсийМетаданных2);
		
	КонецЕсли;
	
	Лог.Отладка("Очищаю каталог выгрузки");
	УдалитьФайлы(КаталогВыгрузки, "*");
	
	Лог.Отладка("Копирую каталог проекта EDT в каталог выгрузки");
	ФС.КопироватьСодержимоеКаталога(КаталогПроекта, КаталогВыгрузки);
	
КонецПроцедуры

#КонецОбласти

#Область Вспомогательные_процедуры_и_функции

Функция ОпределитьИсполняемыйФайл()
	
	ИсполняемыйФайл = "";

	КаталогEDT = УточнитьВерсиюEDT();

	Если Лев(ВерсияEDT, 4) >= "2024" Тогда // BSLLS:MagicNumber-off
		ИсполняемыйФайл = НайтиEdtcli(КаталогEDT);
	Иначе
		ИсполняемыйФайл = НайтиRing();
	КонецЕсли;

	Возврат ИсполняемыйФайл;
	
КонецФункции

Функция УточнитьВерсиюEDT()

	ВсеВерсииEDT = НайтиВсеУстановленныеEDT();

	Если Не ЗначениеЗаполнено(ВерсияEDT) Тогда
		
		Лог.Информация("Версия EDT не передана через параметры, выполняется поиск максимальной установленной версии.");
		
		ВерсияEDT = ВсеВерсииEDT.Версии[0];
		КаталогEDT = ВсеВерсииEDT.КаталогиEDT.Получить(ВсеВерсииEDT.Версии[0]);
		Лог.Информация("Версия EDT определена автоматически: %1, каталог %2", ВерсияEDT, КаталогEDT);

		Возврат КаталогEDT;

	Иначе
		
		Возврат ВсеВерсииEDT.КаталогиEDT.Получить(ВерсияEDT);
	
	КонецЕсли;

КонецФункции

// Функция - получает команду запуска утилиты ring или 1cedtcli для выполнения конвертации
//
// Возвращаемое значение:
//  Команда      - команда запуска утилиты ring или 1cedtcli для выполнения конвертации
//
Функция КомандаКонвертации(КаталогВыгрузки, РабочееПространство, КаталогПроекта)
	
	Команда = Новый Команда;
	Команда.УстановитьКоманду(ОпределитьИсполняемыйФайл());

	Если Лев(ВерсияEDT, 4) >= "2024" Тогда // BSLLS:MagicNumber-off
		КомандаEdtcli(Команда, КаталогВыгрузки, РабочееПространство, КаталогПроекта);
	Иначе
		КомандаRing(Команда, КаталогВыгрузки, РабочееПространство, КаталогПроекта);
	КонецЕсли;
	
	Команда.ДобавитьЛогВыводаКоманды("oscript.lib.gitsync.plugins.edtExport");
	Команда.УстановитьИсполнениеЧерезКомандыСистемы(Истина);
	Команда.ПоказыватьВыводНемедленно(Ложь);
	Команда.УстановитьПравильныйКодВозврата(0);

	Возврат Команда;

КонецФункции

// Функция - получает команду запуска утилиты ring для выполнения конвертации
//
// Возвращаемое значение:
//  Команда      - команда запуска утилиты ring для выполнения конвертации
//
Процедура КомандаRing(Команда, КаталогВыгрузки, РабочееПространство, КаталогПроекта)

	Команда.ДобавитьПараметр(СтрШаблон("edt@%1:x86_64", ВерсияEDT));
	Команда.ДобавитьПараметр("workspace import");
	
	Команда.ДобавитьПараметр(СтрШаблон("--configuration-files ""%1""", КаталогВыгрузки));
	Команда.ДобавитьПараметр(СтрШаблон("--workspace-location ""%1""", РабочееПространство));
	Команда.ДобавитьПараметр(СтрШаблон("--project ""%1""", КаталогПроекта));
	
	Если Не ПустаяСтрока(ИмяРасширения) И Не ПустаяСтрока(ИмяБазовогоПроекта) Тогда
		Команда.ДобавитьПараметр(СтрШаблон("--base-project-name ""%1""", ИмяБазовогоПроекта));
	КонецЕсли;

КонецПроцедуры

// Функция - получает команду запуска утилиты edtcli для выполнения конвертации
//
// Возвращаемое значение:
//  Команда      - команда запуска утилиты edtcli для выполнения конвертации
//
Процедура КомандаEdtcli(Команда, КаталогВыгрузки, РабочееПространство, КаталогПроекта)

	Команда.ДобавитьПараметр("-data");
	Команда.ДобавитьПараметр(ОбернутьВКавычки(РабочееПространство));
	Команда.ДобавитьПараметр("-command import");
	
	Команда.ДобавитьПараметр(СтрШаблон("--project ""%1""", КаталогПроекта));
	Команда.ДобавитьПараметр(СтрШаблон("--configuration-files ""%1""", КаталогВыгрузки));
	
	Если Не ПустаяСтрока(ИмяРасширения) И Не ПустаяСтрока(ИмяБазовогоПроекта) Тогда
		Команда.ДобавитьПараметр(СтрШаблон("--base-project-name ""%1""", ИмяБазовогоПроекта));
	КонецЕсли;

КонецПроцедуры

// Функция - выполняет поиск утилиты ring и возвращает полный путь к утилите
//
// Возвращаемое значение:
//  Строка      - полный путь к утилите ring
//
Функция НайтиRing() Экспорт

	Если ЭтоWindows() Тогда
		КаталогПрограмм = ПолучитьПеременнуюСреды("ProgramW6432");
		Расширение = ".cmd";
	Иначе
		КаталогПрограмм = "/opt";
		Расширение = "";
	КонецЕсли;
	ПутьУстановки_1CE = ОбъединитьПути(КаталогПрограмм, "1C", "1CE", "components");
	Файлы = НайтиФайлы(ПутьУстановки_1CE, СтрШаблон("ring%1", Расширение), Истина);

	Если Файлы.Количество() > 0 Тогда
		Возврат Файлы[0].ПолноеИмя;
	КонецЕсли;

	Возврат "";

КонецФункции

// Функция - выполняет поиск утилиты 1cedtcli и возвращает полный путь к утилите
//
// Возвращаемое значение:
//  Строка      - полный путь к утилите 1cedtcli
//
Функция НайтиEdtcli(КаталогEDT) Экспорт

	Расширение = "";
	Если ЭтоWindows() Тогда
		Расширение = ".exe";
	КонецЕсли;

	ИсполняемыйФайл = СтрШаблон("1cedtcli%1", Расширение);
	Лог.Информация("Ищу %1 в %2", ИсполняемыйФайл, КаталогEDT);
	НайденныеФайлы = НайтиФайлы(КаталогEDT, ИсполняемыйФайл, Истина);

	Если НайденныеФайлы.Количество() = 0 Тогда
		ВызватьИсключение "1cedtcli не найден";
	КонецЕсли;

	Результат = НайденныеФайлы[0].ПолноеИмя;
	Лог.Информация("Найден исполняемый файл %1", Результат);
	Возврат Результат;

КонецФункции

Функция ОбернутьВКавычки(Знач Строка)
	Возврат """" + Строка + """";
КонецФункции

// Функция - проверяет, что скрипт выполняется в среде Windows
//
// Возвращаемое значение:
//	Булево     - Истина - скрипт выполняется в среде Windows
//
Функция ЭтоWindows() Экспорт

	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат Найти(НРег(СистемнаяИнформация.ВерсияОС), "windows") > 0;

КонецФункции // ЭтоWindows()

// Функция - выполняет поиск всех установленных версий EDT
//
// Возвращаемое значение:
//  Структура      - отсортированные версии и пути к исполняемым файлам
//
Функция НайтиВсеУстановленныеEDT()

	КаталогиEDT = Новый Соответствие;
	ВерсииДляСортировки = Новый Массив;

	КаталогиПоиска = КаталогиУстановкиЕДТПоУмолчанию();

	РВ = Новый РегулярноеВыражение("1c[-|_]edt[-| ](\d+\.\d+(?:\.\d)*)");
	РВ.ИгнорироватьРегистр = Истина;

	Для Каждого КаталогПоиска Из КаталогиПоиска Цикл

		Лог.Информация("Ищу версии EDT в каталоге ""%1""", КаталогПоиска);

		ФайлыIni = НайтиФайлы(КаталогПоиска, "1cedt.ini", Истина);
		Файлы = НайтиФайлы(КаталогПоиска, "1cedt", Истина);

		Для Каждого Файл Из ФайлыIni Цикл
			Файлы.Добавить(Файл);
		КонецЦикла;
		
		Для Каждого ТекФайл Из Файлы Цикл

			Лог.Информация("Ищу совпадения регулярного выражения с именем файла ""%1""", ТекФайл.Путь);
			КаталогВерсии = Новый Файл(ТекФайл.Путь);
			Совпадения = РВ.НайтиСовпадения(КаталогВерсии.Имя);

			Если Совпадения.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
		
			НайденнаяВерсия = Совпадения[0].Группы[1].Значение;
			
			КаталогиEDT.Вставить(НайденнаяВерсия, ТекФайл.Путь);
			ВерсииДляСортировки.Добавить(НайденнаяВерсия);

			Лог.Информация("Найдена версия EDT ""%1"" в каталоге ""%2"".", НайденнаяВерсия, ТекФайл.Путь);
		КонецЦикла;
	КонецЦикла;

	Версии.СортироватьВерсии(ВерсииДляСортировки, "УБЫВ");
	
	Если ВерсииДляСортировки.Количество() = 0 Тогда
		ВызватьИсключение "Не найдено ни одной установленной версии EDT";
	КонецЕсли;

	Возврат Новый Структура("Версии, КаталогиEDT", ВерсииДляСортировки, КаталогиEDT);

КонецФункции

// скопировано из yard, код Abramov Dmitry 
Функция КаталогиУстановкиЕДТПоУмолчанию()

	СистемнаяИнформация = Новый СистемнаяИнформация();
	Массив = Новый Массив();
	Если ЭтоWindows() Тогда
		Массив.Добавить(ОбъединитьПути(СистемнаяИнформация.ПолучитьПутьПапки(СпециальнаяПапка.ЛокальныйКаталогДанныхПриложений), "1C\1cedtstart\installations"));
		Массив.Добавить("C:\Program Files\1C\1CE\components");
	Иначе
		Массив.Добавить("~/.local/share/1C/1cedtstart/installations");
		Массив.Добавить("/opt/1C/1CE/components");
	КонецЕсли;

	Возврат Массив;
	
КонецФункции

Процедура ДополнитьИнкрементнуюВыгрузкуПроекта(Конфигуратор, КаталогВыгрузки)
	
	ПутьКФайлуДополнительнойВыгрузки = ВременныеФайлы.НовоеИмяФайла();
	
	СформироватьСписокДополнительныхОбъектов(КаталогВыгрузки, ПутьКФайлуДополнительнойВыгрузки);
	
	Если ФС.ФайлСуществует(ПутьКФайлуДополнительнойВыгрузки) Тогда
		
		Параметры = Конфигуратор.ПолучитьПараметрыЗапуска();
		
		Параметры.Добавить(СтрШаблон("/DumpConfigToFiles ""%1""", КаталогВыгрузки));
		Параметры.Добавить(СтрШаблон("-listFile ""%1""", ПутьКФайлуДополнительнойВыгрузки));
		
		Конфигуратор.ВыполнитьКоманду(Параметры);
		
		ВременныеФайлы.УдалитьФайл(ПутьКФайлуДополнительнойВыгрузки);
		
	КонецЕсли;
	
КонецПроцедуры

// Возращает имя родительского объекта меданных
//
// Параметры:
//   ПолноеИмяОбъекта - Строка - Полное имя объекта метаданных.
//
//  Возвращаемое значение:
//   Строка - имя родительского объекта
//
Функция РодительОбъекта(ПолноеИмяОбъекта)
	
	ЧастиИмени = СтрРазделить(ПолноеИмяОбъекта, ".");
	
	Если ЧастиИмени.Количество() > 1 Тогда
		
		ТипОбъектаМетаданных = ЧастиИмени[0];
		ИмяОбъектаМетаданных = ЧастиИмени[1];
		
	Иначе
		Возврат ПолноеИмяОбъекта;
	КонецЕсли;
	
	Если ЭтоРодительВерхнегоУровня(ТипОбъектаМетаданных) Тогда
		Возврат ТипОбъектаМетаданных;
	Иначе
		Возврат СтрШаблон("%1.%2", ТипОбъектаМетаданных, ИмяОбъектаМетаданных);
	КонецЕсли;
	
КонецФункции

Функция ЭтоВложенныйОбъект(ПолноеИмяОбъекта)
	
	ИндексВложенногоТипа = 2;

	ЧастиИмени = СтрРазделить(ПолноеИмяОбъекта, ".");
	Если ЧастиИмени.Количество() > ИндексВложенногоТипа Тогда
		ТипВложенногоОбъекта = ЧастиИмени[ИндексВложенногоТипа];
	Иначе
		Возврат Ложь;
	КонецЕсли;

	Возврат (СтрНайти("Form,Template,Recalculation,Subsystem", ТипВложенногоОбъекта) > 0);

КонецФункции

Функция ЭтоРодительВерхнегоУровня(Родитель)
	
	Возврат (СтрНайти("Configuration,Language", Родитель) > 0);

КонецФункции

Функция ИзмененныеОбъектыМетаданных(ПутьКФайлуИзменений)
	
	РегулярноеВыражение = Новый РегулярноеВыражение("^(?>New|Modified):\s*(\S+)\s*$");
	
	ЧтениеФайла = Новый ЧтениеТекста(ПутьКФайлуИзменений);
	Совпадения = РегулярноеВыражение.НайтиСовпадения(ЧтениеФайла.Прочитать());
	ЧтениеФайла.Закрыть();
	
	ИзмененныеОбъекты = Новый Соответствие;
	ИзмененныеОбъекты.Вставить("Configuration", Новый Массив);
	ИзмененныеОбъекты.Вставить("Language", Новый Массив);
	
	Для Каждого Совпадение Из Совпадения Цикл
		
		ИмяОбъектаМетаданных = Совпадение.Группы[1].Значение;
		Родитель = РодительОбъекта(ИмяОбъектаМетаданных);
		
		ПодчиненныеОбъекты = ИзмененныеОбъекты.Получить(Родитель);
		Если ПодчиненныеОбъекты = Неопределено Тогда
			ПодчиненныеОбъекты = Новый Массив;
			ИзмененныеОбъекты.Вставить(Родитель, ПодчиненныеОбъекты);
		КонецЕсли;
		
		ПодчиненныеОбъекты.Добавить(ИмяОбъектаМетаданных);
		
	КонецЦикла;
	
	Возврат ИзмененныеОбъекты;
	
КонецФункции

Функция ДополнительныеОбъектыКВыгрузке(ИзмененныеОбъекты, ПутьКФайлуВерсийМетаданных)
	
	СписокОбъектов = Новый Массив;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ПутьКФайлуВерсийМетаданных);

	ЧтениеXML.ПерейтиКСодержимому(); // ConfigDumpInfo
	ЧтениеXML.Прочитать(); // ConfigVersions
	ЧтениеXML.Прочитать(); // Metadata

	Пока ЧтениеXML.Имя = "Metadata" Цикл

		ИмяОбъекта = ЧтениеXML.ЗначениеАтрибута("name");
		Родитель = РодительОбъекта(ИмяОбъекта);

		Если ЭтоВложенныйОбъект(ИмяОбъекта) 
			ИЛИ ИмяОбъекта = Родитель
			ИЛИ ЭтоРодительВерхнегоУровня(Родитель) Тогда

			Изменения = ИзмененныеОбъекты.Получить(Родитель);
			Если Изменения <> Неопределено И Изменения.Найти(ИмяОбъекта) = Неопределено	Тогда
				СписокОбъектов.Добавить(ИмяОбъекта);
			КонецЕсли;

		КонецЕсли;

		ЧтениеXML.Пропустить();
		ЧтениеXML.Прочитать();

	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	
	Возврат СписокОбъектов;
	
КонецФункции

Процедура СформироватьСписокДополнительныхОбъектов(КаталогВыгрузки, ПутьКФайлуДополнительнойВыгрузки)
	
	ПутьКФайлуИзменений = ОбъединитьПути(КаталогВыгрузки, ИмяФайлаИзменений);
	ПутьКФайлуВерсийМетаданных = ОбъединитьПути(КаталогВыгрузки, ИмяФайлаДампаКонфигурации);
	
	ИзмененныеОбъекты = ИзмененныеОбъектыМетаданных(ПутьКФайлуИзменений);
	ДополнительныеОбъектыКВыгрузке = ДополнительныеОбъектыКВыгрузке(ИзмененныеОбъекты, ПутьКФайлуВерсийМетаданных);
	
	Если ДополнительныеОбъектыКВыгрузке.Количество() > 0 Тогда
		
		ФайлОбъектовВыгрузки = Новый ТекстовыйДокумент();
		Для Каждого ДополнительныйОбъект Из ДополнительныеОбъектыКВыгрузке Цикл
			ФайлОбъектовВыгрузки.ДобавитьСтроку(ДополнительныйОбъект);
		КонецЦикла;
		ФайлОбъектовВыгрузки.Записать(ПутьКФайлуДополнительнойВыгрузки, КодировкаТекста.UTF8);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Инициализация()
	
	Лог = Логирование.ПолучитьЛог(ИмяЛога());
	КомандыПлагина = Новый Массив;
	КомандыПлагина.Добавить("sync");
	
	ИмяРасширения = "";
	РабочееПространство = "";
	ИмяБазовогоПроекта = "";
	
	ИмяФайлаДампаКонфигурации = "ConfigDumpInfo.xml";
	ИмяФайлаИзменений = "dumplist.txt"; // файл используется в 2х плагинах - increment и edtExport
	
КонецПроцедуры

#КонецОбласти

Инициализация();